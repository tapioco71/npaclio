#!/usr/local/bin/clisp -q -ansi -Kfull -E utf-8
;;;; -*- mode:lisp;coding:utf-8 -*-
;;;;**************************************************************************
;;;;FILE:               rstpre
;;;;LANGUAGE:           Common-Lisp
;;;;SYSTEM:             Common-Lisp
;;;;USER-INTERFACE:     NONE
;;;;DESCRIPTION
;;;;    
;;;;    Pre-processes rst files.  New directives implemented:
;;;;
;;;;       .. UML path.yuml
;;;;
;;;;           The contents of the file at path sent to yuml.be to be
;;;;           converted into a png, and then the png image is
;;;;           included in place of the UML line.
;;;;    
;;;;AUTHORS
;;;;    <PJB> Pascal J. Bourguignon <pjb@informatimago.com>
;;;;MODIFICATIONS
;;;;    2015-10-28 <PJB> Extracted from old project.
;;;;BUGS
;;;;LEGAL
;;;;    AGPL3
;;;;    
;;;;    Copyright Pascal J. Bourguignon 2015 - 2015
;;;;    
;;;;    This program is free software: you can redistribute it and/or modify
;;;;    it under the terms of the GNU Affero General Public License as published by
;;;;    the Free Software Foundation, either version 3 of the License, or
;;;;    (at your option) any later version.
;;;;    
;;;;    This program is distributed in the hope that it will be useful,
;;;;    but WITHOUT ANY WARRANTY; without even the implied warranty of
;;;;    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;;;    GNU Affero General Public License for more details.
;;;;    
;;;;    You should have received a copy of the GNU Affero General Public License
;;;;    along with this program.  If not, see <http://www.gnu.org/licenses/>.
;;;;**************************************************************************

#+clisp (setf custom:*default-file-encoding* charset:utf-8)

(defparameter *base-url* "http://hubble:3000/diagram/")
(defparameter *base-url* "http://yuml.me/diagram/")

(defun yuml-url (lines &key (style  :nofunky) (scale 100) (direction :lr)
                 (kod :class))
  (check-type style (member :nofunky :plain :scruffy))
  (check-type scale (integer 10 200))
  (check-type direction (member :lr :tb :bt :rl))
  (check-type kod (member :class :activity :usecase))
  (check-type lines list) ; of strings
  (format nil "~A~(~A~);scale:~D;dir:~A/~(~A~)/~{~A~^,~}"
          *base-url* style scale direction kod lines))

(defvar *style*     :plain)
(defvar *scale*     100)
(defvar *direction* :lr)

(defun uml-line-p (line)
  (regexp:match "^\\.\\.  *UML  *\\([^ ][^ ]*\\)" line))

(defun get-resource (file url)
  (let ((cached-url (make-pathname :type "URL" :case :COMMON :defaults file)))
    (if (and (probe-file file)
             (probe-file cached-url)
             (string=  url (with-open-file (stream cached-url) (read-line stream))))
      file
      (progn
        (ignore-errors (delete-file file))
        (print `(ext:run-program "wget" :arguments ,(list "-O" file  url) :wait t :output nil)) (finish-output)
        (or (ext:run-program "wget" :arguments (list "-O" file  url) :wait t
                             :output nil)
            (progn
              (with-open-file (stream cached-url
                                      :direction :output 
                                      :if-exists :supersede
                                      :if-does-not-exist :create) 
                (write-line url stream))
              file))))))


(defun process-uml (line dir src dst)
  (multiple-value-bind (all kod name options)
      (regexp:match "^\\.\\.  *UML  *\\([a-z][a-z]*\\)/\\([^ ][^ ]*\\)\\( .*\\)" line)
    (when all
      (let* ((kod     (regexp:match-string line kod))
             (name    (regexp:match-string line name))
             (options (with-input-from-string (input  (regexp:match-string line options))
                        (loop
                           :for item = (read input nil input)
                           :until (eq item input)
                           :collect item)))
             (file (make-pathname :name (format nil "~A-~A" kod name)
                                  :type "png"
                                  :version nil
                                  :defaults dir)))
        (destructuring-bind (&key (style *style*) (scale *scale*) (direction *direction*)) options
         (format dst ".. |~A/~A| image:: ~A~%" kod name  
                 (namestring
                  (get-resource
                   file
                   (yuml-url
                    (loop 
                       :for line = (read-line src nil nil)
                       :for sline = (and line (string-trim " " line))
                       :while (and line (not (string= "" sline)))
                       :collect sline)
                    :kod (intern (string-upcase kod) "KEYWORD")
                    :style style :scale scale :direction direction)))))))))


(defun process-stream (dir src dst)
  (loop
     :for line = (read-line src nil nil)
     :while line
     :do (if (uml-line-p line)
           (process-uml line dir src dst)
           (write-line line dst))))


(defun main (args)
  (if (null args)
      (process-stream #P"./"  *standard-input* *standard-output*)
      (dolist (src args)
        (let ((dir (make-pathname :name nil :type nil   :version nil :case :common :defaults src))
              (rst (make-pathname           :type "RST" :version nil :case :common :defaults src)))
          (with-open-file (input src)
            (with-open-file (output rst
                                    :direction :output
                                    :if-exists :supersede
                                    :if-does-not-exist :create)
              (process-stream dir input output)))))))

#-testing-script
(main ext:*args*)

(pushnew :testing-script *features*)
;;;; THE END ;;;;
